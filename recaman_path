int total=1;

void setup() {
  size(2000, 1300);
  strokeWeight(7);
  stroke(255,105,180);
  noLoop();
}
  
void draw() {
  IntList path = new IntList();
  int last = 0;
  float [] xplace;
  boolean toggle = false;
  background(255);
  for (int i=0;i<total;i++){
    if(last-i<0||(path.hasValue(last-i))){
      path.append(last+i);
      last=last+i;
    } else {
      path.append(last-i);
      last=last-i;
    }
  } 
  xplace = new float [path.size()];
  for (int i=0; i<path.size(); i++) {
    xplace[i]=width/(path.max()+1)*(path.get(i)+0.5);
  }
  for (int i=0;i<path.size()-1;i++){
    if (toggle==true){
      noFill();
      arc((xplace[i]+xplace[i+1])/2, height/2, abs(xplace[i]-xplace[i+1]), abs(xplace[i]-xplace[i+1]), -PI, 0);
      toggle=false;
    } else {
      noFill();
      arc((xplace[i]+xplace[i+1])/2, height/2, abs(xplace[i]-xplace[i+1]), abs(xplace[i]-xplace[i+1]), 0, PI);
      toggle=true;
    }
  }
}

void keyPressed(){
  total+=1;
  redraw();
}

void mouseClicked(){
    saveFrame("screenshotRecaman#####.png");
  }
